plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.2'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.openapi.generator' version '7.8.0'
}

group = 'org.vaargcapital'
version = '0.0.1-SNAPSHOT'
description = 'evolve'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springdoc:springdoc-openapi-starter-webflux-ui:2.6.0'
    implementation 'org.springframework.boot:spring-boot-starter-flyway'
    implementation 'org.projectlombok:lombok'

    runtimeOnly 'org.postgresql:postgresql'
    runtimeOnly 'org.postgresql:r2dbc-postgresql'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.12.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

openApiGenerate {
    generatorName = 'spring'
    library = 'spring-boot'
    inputSpec = "$projectDir/src/main/resources/openapi/api.yaml"
    outputDir = "$buildDir/generated/openapi"
    apiPackage = 'org.vaargcapital.evolve.api'
    modelPackage = 'org.vaargcapital.evolve.model'
    configOptions = [
            useTags: 'true',
            dateLibrary: 'java8',
            reactive: 'true',
            interfaceOnly: 'true',
            useSpringBoot3: 'true'
    ]
}

sourceSets {
    main {
        java {
            srcDir "$buildDir/generated/openapi/src/main/java"
        }
    }
}

tasks.named('compileJava') {
    dependsOn tasks.named('openApiGenerate')
}

tasks.named('test') {
    useJUnitPlatform()
}
